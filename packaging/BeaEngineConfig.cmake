cmake_minimum_required(VERSION 3.19)

set(BeaEngine_known_comps static shared)
set(BeaEngine_comp_static NO)
set(BeaEngine_comp_shared NO)
foreach (BeaEngine_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (BeaEngine_comp IN_LIST BeaEngine_known_comps)
        set(BeaEngine_comp_${BeaEngine_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
                "bsw does not recognize component `${BeaEngine_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (BeaEngine_comp_static AND BeaEngine_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "bsw `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(BeaEngine_static_targets "${CMAKE_CURRENT_LIST_DIR}/bsw-static-targets.cmake")
set(BeaEngine_shared_targets "${CMAKE_CURRENT_LIST_DIR}/bsw-shared-targets.cmake")

macro(BeaEngine_load_targets type)
    if (NOT EXISTS "${BeaEngine_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
                "bsw `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${BeaEngine_${type}_targets}")
endmacro()

if (BeaEngine_comp_static)
    BeaEngine_load_targets(static)
elseif (BeaEngine_comp_shared)
    BeaEngine_load_targets(shared)
elseif (DEFINED BeaEngine_SHARED_LIBS AND BeaEngine_SHARED_LIBS)
    BeaEngine_load_targets(shared)
elseif (DEFINED BeaEngine_SHARED_LIBS AND NOT BeaEngine_SHARED_LIBS)
    BeaEngine_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${BeaEngine_shared_targets}")
        BeaEngine_load_targets(shared)
    else ()
        BeaEngine_load_targets(static)
    endif ()
else ()
    if (EXISTS "${BeaEngine_static_targets}")
        BeaEngine_load_targets(static)
    else ()
        BeaEngine_load_targets(shared)
    endif ()
endif ()